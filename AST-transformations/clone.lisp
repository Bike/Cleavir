(cl:in-package #:cleavir-ast-transformations)

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;
;;; Cloning an AST.

;;; In step one of cloning an AST, we create a dictionary mapping
;;; every node in the original AST to a cloned node.  The cloned node
;;; is created by calling MAKE-INSTANCE on the class of the original
;;; node.  No initialization arguments are passed to MAKE-INSTANCE, so
;;; the cloned node is uninitialized except for slots that are
;;; initialized by :INITFORM.
(defun clone-create-dictionary (ast)
  (let ((dictionary (make-hash-table :test #'eq)))
    (labels ((traverse (node)
	       (when (null (gethash node dictionary))
		 (setf (gethash node dictionary)
		       (make-instance (class-of node)))
		 (mapc #'traverse (cleavir-ast:children node)))))
      (traverse ast))
    dictionary))

;;; This generic function is used to obtain some substructure
;;; (typically a slot) for the new AST, given the corresponding
;;; substructure of the model AST.
(defgeneric finalize-substructure (object dictionary))

;;; For most objects, such as numbers, symbols, strings, etc, the
;;; object to be used in the new AST is the same as the one in the
;;; original AST.
(defmethod finalize-substructure (object dictionary)
  (declare (ignore dictionary))
  object)

;;; If the substructure of an AST is a CONS (typically a proper list),
;;; then we obtain the corresponding substructure of the new AST by
;;; copying the CONS and calling FINALIZE-SUBSTRUCTURE on the CAR and
;;; the CDR.
(defmethod finalize-substructure ((object cons) dictionary)
  (cons (finalize-substructure (car object) dictionary)
	(finalize-substructure (cdr object) dictionary)))

;;; If the substructure of an AST is another AST, then the
;;; corresponding substructure of the new AST is obtained from the
;;; dictionary.
(defmethod finalize-substructure ((object cleavir-ast:ast) dictionary)
  (gethash object dictionary))

;;; Given an AST to finalize and the MODEL AST of which the AST is a
;;; clone,  reinitialize the AST with new substructure.
(defun finalize (ast model dictionary)
  (apply #'reinitialize-instance
	 ast
	 (loop for (keyword reader) in (cleavir-io:save-info model)
	       for value = (funcall reader model)
	       collect keyword
	       collect (finalize-substructure value dictionary))))

(defun clone-ast (ast)
  (let ((dictionary (clone-create-dictionary ast)))
    (maphash (lambda (key value)
	       (finalize value key dictionary))
	     dictionary)
    (gethash ast dictionary)))

;;; This function is a variation on the cloning.  Instead of directly
;;; computing a clone of an AST, it creates a PROGRAM that, when
;;; executed, creates a clone of the AST.  This feature is useful for
;;; FASL generation.  We can compile the code generated by this
;;; function as part of the FASL so that the AST is generated when the
;;; FASL is loaded.  That way, we can get inline information for
;;; functions loaded from FASL files.
(defun make-create-ast-code (ast)
  (let ((dictionary (make-hash-table :test #'eq)))
    (labels
	((traverse (node)
	   (when (null (gethash node dictionary))
	     (setf (gethash node dictionary) (gensym))
	     (mapc #'traverse (cleavir-ast:children node)))))
      (traverse ast))
    (flet ((init-args-1 (node)
	     (loop for (keyword reader) in (cleavir-io:save-info node)
		   unless (typep (funcall reader node) 'cleavir-ast:ast)
		     collect keyword
		     and collect `',(funcall reader node)))
	   (init-args-2 (node)
	     (loop for (keyword reader) in (cleavir-io:save-info node)
		   when (typep (funcall reader node) 'cleavir-ast:ast)
		     collect keyword
		     and collect (gethash (funcall reader node) dictionary))))
      `(let ,(loop for key being each hash-key of dictionary
		     using (hash-value value)
		   collect `(,value
			     (make-instance
				 ',(class-name (class-of key))
			       ,@(init-args-1 key))))
	 ,@(loop for key being each hash-key of dictionary
		   using (hash-value value)
		 collect `(reinitialize-instance
			   ,(gethash value dictionary)
			   ,@(init-args-2 key)))
	 ,(gethash ast dictionary)))))
